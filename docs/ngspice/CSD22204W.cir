*$
*CSD22204W
***********************************************************************
* (C) Copyright 2015 Texas Instruments Incorporated. All rights reserved.
***********************************************************************
***********************************************************************
** This model is designed as an aid for customers of Texas Instruments.
** TI and its licensors and suppliers make no warranties, either expressed
** or implied, with respect to this model, including the warranties of
** merchantability or fitness for a particular purpose. The model is
** provided solely on an "as is" basis. The entire risk as to its quality
** and performance is with the customer
********************************************************************************
**Released by: WEBENCH(R) Design Center, Texas Instruments Inc.
* Part: CSD22204W
* Date: 03/24/2015
* Model Type: TRANSIENT
* Simulator: PSPICE
* Simulator Version: 16.2.0.p001
* EVM Order Number:
* EVM Users Guide:
* Datasheet: SLPS559–MARCH 2015
* Model Version: Final 1.10
**
*****************************************************************************
*
* Updates:
*<Made these changed to the model>
*		1. NMOS Symbol changed to PMOS
*		2. The maximuim step was left blank from earlier 2ns for simulator to decide the time step.
*
* Final 1.10
* Release to Web
*
**********************************************************************
**********************************************************************
*                                                                    *
* CSD22204W   -  PSpice Model for use with OrCAD                     *
*                                                                    *
**********************************************************************
**********************************************************************
* Characteristics:
*   Technology:   PCH 08/06
*
**********************************************************************
* Model History: characterization
*   tr, 1:32 PM 3/13/2015
*    
**********************************************************************
* SUBCKT Definition:  1=D  2=G  3=S
.SUBCKT	CSD22204W	1	2	3
* PARAMETER (local) DEFINITIONS SECTION
 .param	 AAREF =		1.000
 .param	 AAP68827 =	1.125
 .PARAM  ptrc1 =   	0.00e-3  
 .PARAM  ptrc2 =   	0.00e-6
 .PARAM  pwidthP = 	{AAP68827/0.8*2}
 .PARAM  pwidth =  	{pwidthP*1e6}
 .PARAM  perimP =  	{2.1*pwidthP}
 .PARAM  perim =   	{perimP*1e6}
****** MODEL SUBCIRCUIT BEGINS HERE     ******************************
RDD		1	4	RTEMP	4.60e-3
RGG		2	5	600
RSS		6	3	0.1e-3
M1		4	5	6	6	PMOSDev	W={pwidthP} L=0.5u  PS={perimP} PD={perimP}

DBD		4	6	DBD	{AAP68827/AAREF}

DDG  		5	7	DD	{AAP68827/AAREF}
DGD  		4	7	DD	{AAP68827/AAREF}

DGS		2	8	DDZ
DSG		9	8	DDZ

RESD	9	3	70

******************************************************************* 
.MODEL  PMOSDev   PMOS (
* CONTROL PARAMETERS
+ LEVEL  = 6             VERSION = 3.2         CAPMOD = 2
* DC PARAMETERS
+ NLX    = 1.74E-7       VBM    = -5           DVT0   = 2.2
+ DVT1   = 0.53          DVT2   = -0.032       U0     = 250
+ UA     = 8.0e-9        UB     = 0.0e-19      A0     = 0.9
+ AGS    = 0.0           A1     = 0.0          A2     = 0.9
+ LINT   = 0.055e-6      VOFF   = -0.22        NFACTOR= 1.7
+ ETA0   = 0.005         ETAB   = -0.07        DSUB   = 1.4
+ PCLM   = 3.00          PDIBLC1= 0.02         PDIBLC2= 0.004
+ DROUT  = 0.90          PSCBE1 = 3.0e8        PSCBE2 = 1e-6
+ PVAG   = 0.1           DELTA  = 0.0220       JS     = 0
* AC AND CAPACITANCE PARAMETERS
+ CGSO   = 1.00e-24		 CGDO   = 1e-24        CGBO   = 1e-24
+ CJ     = 1e-24         CJSW   = 1e-24        CF     = 0
* TEMPERATURE PARAMETERS
+ TNOM   = 25            UTE    = -1.5         KT1    = -0.70
+ KT1L   = 1.0e-15       KT2    = 0.022        UA1    = -01.00e-9
+ UB1    = 3.0e-17      
* PROCESS PARAMETERS
+ TOX    = 2.50e-8       XJ     = 3e-7         NCH    = 1.10e17
+ NSUB   = 1.0e16)
******************************************************************* 
.MODEL DBD D (CJO=0.9e-09 VJ=0.95 M=0.45 TNOM=25
+ FC=0.5 TT=2e-09 XTI=-2.0 BV=8.8
+ IS=5.0e-9 N=1.3 RS=10.4e-3 TRS1=4.0e-3)
*******************************************************************
.MODEL DD  D (CJO=7.00e-010 VJ=0.4 M=0.45 IS=1e-13 RS=0.1 TNOM=25)
*******************************************************************
.MODEL DDZ D (CJO=1e-011 VJ=0.9 M=0.9 IS=1e-13 RS=1.0e-3 TNOM=25 bv=6.6)
*******************************************************************
.MODEL RTEMP RES (TC1={ptrc1} TC2={ptrc2})
*******************************************************************
.ENDS
